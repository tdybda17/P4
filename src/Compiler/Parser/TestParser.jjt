/**
 * JavaCC template file created by SF JavaCC plugin 1.5.28+ wizard for JavaCC 1.5.0+
 */


options
{
  NODE_DEFAULT_VOID = true;
  STATIC = false;
  VISITOR = true;
  MULTI = true;
  TRACK_TOKENS = true;
}

PARSER_BEGIN(TestParser)

package Compiler.Parser.GeneratedFiles;

import Compiler.Parser.CustomVisitors.*;
import Compiler.Parser.TreePrinter;
import Compiler.SymbolTable.Table.SymbolTable;
import java.io.BufferedReader;
import java.io.File;
import java.io.FileReader;

public class TestParser {
    public static void main(String[] args) {
        try {
//            System.out.println(parseTextFile("src/Compiler/Parser/test"));
//            System.out.println("\n\n\n");
            SymbolTable st = new SymbolTable();
            st.openScope();
            Node root = useVisitorMethods("src/Compiler/Parser/test", st, new TreeOptimizerVisitor(), new FunctionVisitor(), new StaticSemanticsVisitor(st), new CodeGenerationVisitor());
//            System.out.println(TreePrinter.createDotOutput(root));
        } catch (Exception e) {
            e.printStackTrace();
        }
    }

    public static String parseTextFile(String filePath) throws Exception {
        Node root = createParseTree(filePath);
        return TreePrinter.createDotOutput(root);
  }

    public static Node useVisitorMethods(String filePath, Object data, TestParserVisitor... visitors) throws Exception {
        Node root = createParseTree(filePath);
        for (int i = 0; i < visitors.length; i++) {
            root.jjtAccept(visitors[i], data);
        }
        return root;
    }

    public static Node createParseTree(String filePath) throws Exception {
        File file = new File(filePath);
        BufferedReader reader = new BufferedReader(new FileReader(file));
        TestParser parser = new TestParser(reader);
        return parser.start();
    }
}
  


PARSER_END(TestParser)

SKIP :
{
  " "
| "\r"
| "\t"
| < "//" (~["\n"])* > //This is for skipping single-line comment
| < "/*" > : ML_COMMENT_STATE //This is for skipping multi-line comment
}

<ML_COMMENT_STATE> SKIP :
{
  < "*/" > : DEFAULT
| < ~[] >
}

TOKEN : /* OPERATORS */
{
  < CREATE : "create" >
| < END : "end" >
| < BLANK : " " | "\t" >
| < NEWLINE : "\n" >
| < COMMA : "," >
| < DOT : "." >
| < MAIN : "main" >
| < ENDOFFILE : "<EOF>" >
| < ASSIGN : "=" >
| < AND : "&" >
| < OR : "|" >
| < NEG : "!" >
| < FUNCTION : "function" >
| < WHILE : "while" >
| < FOR : "for" >
| < FOREACH : "foreach" >
| < IN : "in" >
| < IF : "if" >
| < THEN : "then" >
| < ELSE : "else" >
| < LCURLY : "{" >
| < RCURLY : "}" >
| < LHARD : "[" >
| < RHARD : "]" >
| < LPAREN : "(" >
| < RPAREN : ")" >
| < CONNECTION : "--" >
| < VERTEX : "Vertex" >
| < EDGE : "Edge" >
| < DIEDGE : "DiEdge" >
| < INT : "int" >
| < REAL : "real" >
| < BOOLEAN : "boolean" >
| < BOOL_VAL : "true" | "false" >
| < DO : "do" >
| < TO : "to" >
| < RETURN : "return" >
| < DCL : "dcl" >
| < MAP : "Map" >
| < COLOR : "Color" >
| < LABEL : "Label" >
}

TOKEN :
{
  < GRAPH_TYPE : (< GRAPH >) >
| < #GRAPH : "Graph" | "DiGraph" >
| < CONSTANTS : (< CONSTANT >) >
| < #CONSTANT : "INF" | "NEG_INF" | "Ã˜" >
| < ADD_SUB : (< addsub >) >
| < #addsub : "+" | "-" >
| < MUL_DIV_MOD : (< muldiv >) >
| < #muldiv : "*" | "/" | "%" >
| < EQUALITY_OP : (< equality_ope >) >
| < #equality_ope : "==" | "!=" >
| < RELATIONAL_OP : (< relational_ope >) >
| < #relational_ope : "<=" | ">=" >
| < LANGLE : "<" >
| < RANGLE : ">" >
| < COLLECTION_TYPE : (< collection >) >
| < #collection : "Set" | "Queue" | "MinQueue" | "MaxQueue" | "Stack" >
| < COLOR_VAL : "GRAY" | "BLACK" | "WHITE" | "RED" | "GREEN" | "BLUE" | "YELLOW" >
| < INUM_VAL : (< DIGIT >) >
| < #DIGIT : "0" | ["1"-"9"](["0"-"9"])* >
| < FNUM_VAL : (< DIGITF >) >
| < #DIGITF : ("0" | ["1"-"9"](["0"-"9"])*)"."(["0"-"9"])+ >
| < IDENTIFIER : (< ID >) >
| < #ID : ["a"-"z", "A"-"Z"](["a"-"z", "A"-"Z"] | ["0"-"9"])* >
| < LABEL_VAL : "\"" (["a"-"z", "A"-"Z"] | ["0"-"9"])+ "\"" >


}

JAVACODE
void skip_to_first_dcl() { 
      Token tok;
      while (true) {
        tok = getToken(1);
        if (tok.kind != NEWLINE)
            break;
        tok = getNextToken();
      }
}

JAVACODE
void skip_to_EOF() {
      Token tok;
      while (true) {
        tok = getToken(1);
        if (tok.kind == EOF)
            break;
        else if (tok.kind != NEWLINE)
            throw new ParseException("Encountered '" + tok.kind + "'. Was expecting: '<NEWLINE>' or '<EOF>'");
        tok = getNextToken();
      }
}



SimpleNode start() #START :
{}
{
  prog() {return jjtThis;}
}

void prog() #PROG(>1) :
{}
{
    skip_to_first_dcl() vertex_edge_attributes() main() newlines_plus() funcs_dcl() #FUNCS_DCL(>1) skip_to_EOF() <EOF>
}

void newlines_plus() #void :
{}
{
    <NEWLINE> newlines_star()
}

void newlines_star() #void :
{}
{
    <NEWLINE> newlines_star() | empty()
}

void vertex_edge_attributes() #VERTEX_EDGE_ATTR(>1) :
{}
{
    vertex_attributes() | edge_attributes() | newlines_star()
}

void vertex_attributes() #void :
{}
{
    vertex_attr_dcl() newlines_star() vertex_attr_tail()
}

void vertex_attr_dcl() #VERTEX_ATTRIBUTES :
{}
{
    <VERTEX> <DCL> newlines_plus() attribute_dcl() <END>
}

void vertex_attr_tail() #void :
{}
{
    edge_attr_dcl() newlines_star() | empty()
}

void edge_attributes() #void :
{}
{
    edge_attr_dcl()  newlines_star() edge_attr_tail()
}

void edge_attr_dcl() #EDGE_ATTRIBUTES :
{}
{
    <EDGE> <DCL> newlines_plus() attribute_dcl() <END>
}

void edge_attr_tail() #void :
{}
{
    vertex_attr_dcl() newlines_star() | empty()
}

void attribute_dcl() #void :
{}
{
    (type() identifier()) #ATTRIBUTES_DCL newlines_plus() attribute_dcl() | empty()
}

void simple_types_dcl() #SIMPLE_DCL :
{}
{
    simple_types() identifier() initialization()
}

void simple_types() #SIMPLE_TYPES :
{Token t;}
{
    t = < INT > {jjtThis.value = t.image;} | t = < REAL > {jjtThis.value = t.image;} | t = < BOOLEAN > {jjtThis.value = t.image;} | t = <LABEL> {jjtThis.value = t.image;} | t = <COLOR> {jjtThis.value = t.image;} | t = <VERTEX> {jjtThis.value = t.image;} | t = <EDGE> {jjtThis.value = t.image;} | t = <DIEDGE> {jjtThis.value = t.image;}
}

void identifier() #IDENTIFIER :
{Token t;}
{
    t = < IDENTIFIER > {jjtThis.value = t.image;}
}

void initialization() #void :
{}
{
    < ASSIGN > expr() #INITIALIZATION | empty()
}

void object_types() #void :
{}
{
    graph_type() | collection_type() | map()
}

void map() #MAP :
{}
{
    <MAP> <LANGLE> type() <COMMA> type() <RANGLE>
}

void collection_type() #COLLECTION_TYPE :
{Token t;}
{
    t = <COLLECTION_TYPE> <LANGLE> simple_types() <RANGLE> {jjtThis.value = t.image;}
}

void expr() #OR_EXPR(>1) :
{}
{
    and_expr() A()
}

void A() #void :
{}
{
    < OR > and_expr() A() | empty()
}

void and_expr() #AND_EXPR(>1) :
{}
{
    equal_expr() B()
}

void B() #void :
{}
{
    < AND > equal_expr() B() | empty()
}

void equal_expr() #EQUAL_EXPR(>1) :
{Token t;}
{
    rel_expr() t = equal_expr1() {jjtThis.value = t.image;}
}

Token equal_expr1() #void :
{Token t;}
{
    t = < EQUALITY_OP > rel_expr() {return t;} | empty() {return new Token();}
}

void rel_expr() #REL_EXPR(>1) :
{Token t;}
{
    arith_expr() t = rel_expr1() {jjtThis.value = t.image;}
}

Token rel_expr1() #void :
{Token t;}
{
  t = relational_op() arith_expr() {return t;} | empty() {return new Token();}
}

Token relational_op() #void :
{Token t;}
{
    t = <LANGLE> {return t;} | t = <RANGLE> {return t;} | t = <RELATIONAL_OP> {return t;}
}

void arith_expr() #ADD_SUB(>1) :
{Token t;}
{
  term() t = C() {jjtThis.value = t.image;}
}

Token C() #void :
{Token t;}
{
  t = < ADD_SUB > term() C() {return t;} | empty() {return new Token();}
}

void term() #MUL_DIV_MOD(>1) :
{Token t;}
{
    neg_expr() t = D() {jjtThis.value = t.image;}
}

Token D() #void :
{Token t;}
{
    t = < MUL_DIV_MOD > atomic_expr() D() {return t;} | empty() {return new Token();}
}

void neg_expr() #void :
{Token t;}
{
   < NEG > atomic_expr() #NEG_EXPR | atomic_expr()
}

void atomic_expr() #void :
{}
{
    < LPAREN > expr() < RPAREN > | value()
}

void value() #void :
{Token t;}
{
    variable_or_function() | inum_val() | fnum_val() | bool_val() | constant_val() | color_val() | label_val()
}

void inum_val() #INUM_VAL :
{Token t;}
{
    t = <INUM_VAL> {jjtThis.value = t.image;}
}

void fnum_val() #FNUM_VAL :
{Token t;}
{
    t = <FNUM_VAL> {jjtThis.value = t.image;}
}

void bool_val() #BOOL_VAL :
{Token t;}
{
    t = <BOOL_VAL> {jjtThis.value = t.image;}
}

void constant_val() #CONSTANT_VAL :
{Token t;}
{
    t = <CONSTANTS> {jjtThis.value = t.image;}
}

void color_val() #COLOR_VAL :
{Token t;}
{
    t = <COLOR_VAL> {jjtThis.value = t.image;}
}

void label_val() #LABEL_VAL :
{Token t;}
{
    t = <LABEL_VAL> {jjtThis.value = t.image;}
}

void variable_or_function() #void :
{int varFieldFunc = 0;}
{
    ((identifier() varFieldFunc = variable_or_function_tail()) #VARIABLE(varFieldFunc == 0 || varFieldFunc == 1)) #FUNCTION_CALL(varFieldFunc == 2)
}

int variable_or_function_tail() #void :
{}
{
    field_access() {return 1;} | function_call() {return 2;} | empty() {return 0;}
}

void field_access() #void :
{int varFieldFunc = 0;}
{
    ((<DOT> identifier() varFieldFunc = variable_or_function_tail()) #FIELD_ACCESS(varFieldFunc == 0 || varFieldFunc == 1)) #FUNCTION_CALL(varFieldFunc == 2)
}

void function_call() #void :
{}
{
    <LPAREN> actual_parameters() <RPAREN> function_tail()
}

void function_tail() #void :
{}
{
    field_access() | empty()
}

void actual_parameters() #ACTUAL_PARAMETERS :
{}
{
    actual_parameter_list() | empty()
}

void actual_parameter_list() #void :
{}
{
    expr() actual_parameter_list1()
}

void actual_parameter_list1() #void :
{}
{
    < COMMA > actual_parameter_list() | empty()
}

void main() #MAIN :
{}
{
    <FUNCTION > < MAIN > < LPAREN > < RPAREN > < NEWLINE > block() #BLOCK < END >
}

void block() #void :
{}
{
    block_content() < NEWLINE > block() | empty()
}

void block_content() #void :
{}
{
    dcl() | stmt() | empty()
}

void dcl() #DCL :
{}
{
    simple_types_dcl() | create()
}

void create() #void :
{}
{
    < CREATE > adt() < END >
}

void adt() #void :
{}
{
    graph_adt() | collection_adt() | map_adt()
}

void graph_adt() #GRAPH_DCL :
{}
{
    graph_type() identifier() graph_dcl()
}

void graph_type() #GRAPH_TYPE :
{Token t;}
{
    t = <GRAPH_TYPE> {jjtThis.value = t.image;}
}

void graph_dcl() #void :
{}
{
    (< ASSIGN > variable_or_function()) #GRAPH_ASSIGN | (newlines_plus() graph_vertex_dcl() newlines_plus() graph_elements()) #GRAPH_DCL_ELEMENTS | empty()
}

void graph_elements() #void :
{}
{
    graph_vertex_dcl() newlines_plus() graph_elements() | empty()
}

void graph_vertex_dcl() #GRAPH_VERTEX_DCL :
{}
{
    identifier() edge_dcl()
}

void edge_dcl() #void :
{}
{
    < CONNECTION > graph_vertex_dcl1() | empty()
}

void graph_vertex_dcl1() #void :
{}
{
    < LCURLY > vertex_list() #VERTEX_LIST < RCURLY > | identifier() weight()
}

void vertex_list() #void :
{}
{
    (identifier() weight()) #VERTEX vertex_list_continuous()
}

void vertex_list_continuous() #void :
{}
{
    < COMMA > vertex_list() | empty()
}

void weight() #WEIGHT :
{}
{
    < LHARD > arith_expr() < RHARD > | empty()
}

void stmt() #void :
{boolean isAssign = false;}
{
    ((variable_or_function() isAssign = assign_or_func()) #ASSIGN(isAssign)) #FUNCTION_CALL_STMT(!isAssign) | loop() | if_statement() | return_statement()
}

boolean assign_or_func() #void :
{}
{
    assignment() {return true;} | empty() {return false;}
}

void assignment() #void :
{}
{
    < ASSIGN > expr()
}

void loop() #void :
{}
{
  while_statement() | for_statement() | foreach_statement()
}

void while_statement() #WHILE_STATEMENT :
{}
{
    < WHILE > expr() < DO > < NEWLINE > block() #BLOCK < END >
}

void for_statement() #FOR_STATEMENT :
{}
{
    < FOR > identifier() < ASSIGN > integer_value() < TO > integer_value() < DO > < NEWLINE > block() #BLOCK < END >
}

void integer_value() #void :
{}
{
    variable_or_function() | inum_val()
}

void foreach_statement() #FOREACH_STATEMENT :
{}
{
    < FOREACH > identifier() < IN > variable_or_function() < DO > < NEWLINE > block() #BLOCK < END >
}

void if_statement() #IF_STATEMENT :
{}
{
    < IF > expr() < THEN > <NEWLINE> block() #BLOCK else_statement()
}

void else_statement() #ELSE_STATEMENT :
{}
{
    < ELSE > block() #BLOCK < END > | < END >
}

void return_statement() #RETURN_STMT :
{}
{
    < RETURN > return_expr()
}

void return_expr() #void :
{}
{
    expr() | empty()
}

void collection_adt() #COLLECTION_ADT :
{}
{
    collection_type() identifier() collection_dcl()
}

void collection_dcl() #void :
{}
{
   < ASSIGN > variable_or_function() #COLLECTION_ASSIGN | newlines_plus() element_list() #ELEMENT_LIST | empty()
}

void element_list() #void :
{}
{
    expr() newlines_plus() element_list_continuous()
}

void element_list_continuous() #void :
{}
{
    element_list() | empty()
}

void map_adt() #MAP_ADT :
{}
{
    <MAP> <LANGLE> type() <COMMA> type() <RANGLE> identifier() map_dcl()
}

void map_dcl() #void :
{}
{
    <ASSIGN> variable_or_function() #MAP_ASSIGN | newlines_plus() map_element_list() #MAP_ELEMENT_LIST | empty()
}

void map_element_list() #void :
{}
{
    (value() <CONNECTION> value()) #KEY_VALUE_PAIR newlines_plus() map_element_list_continuous()
}

void map_element_list_continuous() #void :
{}
{
    map_element_list() | empty()
}

void funcs_dcl() #void :
{}
{
    func_dcl() newlines_plus() funcs_dcl() | empty()
}

void func_dcl() #FUNC_DCL :
{}
{
    ret() < FUNCTION > identifier() < LPAREN > formal_parameters() < RPAREN > <NEWLINE> block() #BLOCK < END >
}

void ret() #RETURN_TYPE :
{}
{
    type() | empty()
}

void formal_parameters() #FORMAL_PARAMETERS :
{}
{
    formal_parameter_list() | empty()
}

void formal_parameter_list() #void :
{}
{
    formal_parameter() formal_parameter_list1()
}

void formal_parameter_list1() #void :
{}
{
    < COMMA > formal_parameter_list() | empty()
}

void formal_parameter() #FORMAL_PARAMETER :
{}
{
    type() identifier()
}

void type() #void :
{}
{
    object_types() | simple_types()
}

void empty() #void : {}
{
     {}
}






