Vertex dcl
    boolean isVisited
    Vertex previous
end

function main()
    create DiGraph g
        a -- {b[2], c[3], d[4]}
        b -- e[7]
        c -- f[6]
        d -- g[4]
        e -- l[6]
        f -- l[2]
        g -- l[3]
    end
    Dijkstra(g, g.getVertex("a"))
end

function Dijkstra(DiGraph g, Vertex source)
    foreach v in g.vertices do
        v.distance = 10000
    end
    source.distance = 0

    create MinQueue<Vertex> Q end
    foreach v in g.vertices do
        Q.insert(v)
    end

    while Q.isEmpty() == false do
        Vertex u = Q.extractMin()
        u.color = GRAY
        g.print()
        u.isVisited = true

        foreach v in g.getNeighbours(u) do
            if v.isVisited == false then
                real alt = u.distance + g.getEdge(u, v).weight
                if alt < v.distance then
                    v.distance = alt
                    v.previous = u
                end
            end
        end
        u.color = BLACK
        g.print()
    end
end
